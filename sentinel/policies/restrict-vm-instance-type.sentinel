import "tfplan/v2" as tfplan
import "strings"

# Read Terraform variable
env = ""
if "env" in tfplan.variables {
  env = strings.to_lower(tfplan.variables["env"].value)
}

allowed_types = [
  "e2-medium",
  "e2-standard-4",
]

compute_instances = filter tfplan.resource_changes as _, rc {
  rc.type is "google_compute_instance" and
  rc.mode is "managed" and
  rc.change.after is not null
}

violations = []

if env is "dev" {

  for compute_instances as _, rc {
    machine_type = rc.change.after.machine_type

    if machine_type not in allowed_types {
      violation = {
        "address": rc.address,
        "machine_type": machine_type,
        "message": "When var.env=dev, machine_type must be one of: " +
                   strings.join(allowed_types, ", "),
      }

      # Sentinel way to append to a list
      violations = violations + [violation]
    }
  }
}

main = rule {
  length(violations) is 0
}

# Nice output
for violations as _, v {
  print(v.address + " -> " + v.message + " (found: " + v.machine_type + ")")
}
